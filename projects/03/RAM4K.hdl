// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/3/b/RAM4K.hdl
/**
 * Memory of 4K 16-bit registers.
 * If load is asserted, the value of the register selected by
 * address is set to in; Otherwise, the value does not change.
 * The value of the selected register is emitted by out.
 */
CHIP RAM4K {
    IN in[16], load, address[12];
    OUT out[16];

    PARTS:
    DMux8Way(in=load, 
        sel=address[0..2], 
        a=loadRam0, 
        b=loadRam1, 
        c=loadRam2, 
        d=loadRam3, 
        e=loadRam4, 
        f=loadRam5, 
        g=loadRam6, 
        h=loadRam7);

    RAM512(in=in, load=loadRam0, address=address[3..11], out=ram512out0);
    RAM512(in=in, load=loadRam1, address=address[3..11], out=ram512out1);
    RAM512(in=in, load=loadRam2, address=address[3..11], out=ram512out2);
    RAM512(in=in, load=loadRam3, address=address[3..11], out=ram512out3);
    RAM512(in=in, load=loadRam4, address=address[3..11], out=ram512out4);
    RAM512(in=in, load=loadRam5, address=address[3..11], out=ram512out5);
    RAM512(in=in, load=loadRam6, address=address[3..11], out=ram512out6);
    RAM512(in=in, load=loadRam7, address=address[3..11], out=ram512out7);

    Mux8Way16(a=ram512out0, 
        b=ram512out1, 
        c=ram512out2, 
        d=ram512out3, 
        e=ram512out4, 
        f=ram512out5, 
        g=ram512out6, 
        h=ram512out7, 
        sel=address[0..2], 
        out=out);
}