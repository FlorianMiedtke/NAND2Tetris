// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:

    //zx
    Mux16(a=x, b=false, sel=zx, out=outZx);

    //nx
    Not16(in=outZx, out=outNx);
    Mux16(a=outZx , b=outNx , sel=nx , out=OutMuxZxNx );

    //zy
    Mux16(a=y, b=false, sel=zy, out=outZy);

    //ny
    Not16(in=outZy, out=outNy);
    Mux16(a=outZy, b=outNy , sel=ny , out=OutMuxZyNy );

    //f
    And16(a=OutMuxZxNx, b=OutMuxZyNy, out=outAnd);
    Add16(a=OutMuxZxNx, b=OutMuxZyNy, out=outAdd);
    Mux16(a=outAnd , b=outAdd , sel=f , out=outF);

    //no
    Not16(in=outF , out=outNo);
    Mux16(a=outF , b=outNo , sel=no , out=out,
    //these outs are for zr
    out[0..7]=outLsbHalf, 
    out[8..14]=outMsbHalf, 
    out[15]=outMsb,

    //ng 
    //MSB shows if number is negative
    out[15]=ng);

    //zr
    //I wanted to make a Nor16Way, but did not find out how to make helper chips. So I used this workaround.
    Or8Way(in=outLsbHalf , out=outOr8WayLsb);
    Or8Way(in[0..6]=outMsbHalf, in[7]=outMsb , out=outOr8WayMsb);
    Or(a=outOr8WayLsb, b=outOr8WayMsb, out=combinedOr);
    Not(in=combinedOr, out=zr);
}